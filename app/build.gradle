plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.inmoglass.launcher"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.08"

        ndk {
            // 设置支持的SO库架构（开发者可以根据需要，选择一个或多个平台的so）
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86","x86_64"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        release {
            storeFile file('../keystore/air_release_system.keystore')
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }

        debug {
            storeFile file('../keystore/air_release_system.keystore')
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "INMO-Launcher-V${variant.versionName}-T${releaseTime()}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets{
        main{
            jniLibs.srcDir 'libs'
            jni.srcDirs = []    //disable automatic ndk-build
        }
    }
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    api(name: 'library-spp', ext: 'aar')
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // util 工具类 使用： 屏幕适配等 教程：https://blankj.com/2016/07/31/android-utils-code/
    implementation 'com.blankj:utilcodex:1.30.6'
    // air系列common
    implementation 'com.github.zhangxianpeng:InmoAirComponent:1.0.4'
    // data包
    implementation 'com.github.zhangxianpeng:InmoData:1.0.3'
    // 消息传递
    implementation 'org.greenrobot:eventbus:3.2.0'
    // 数据持久化
    implementation 'com.tencent:mmkv-static:1.2.7'
    // 图片加载
    implementation 'jp.wasabeef:glide-transformations:4.0.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    // 百度地图
    implementation 'com.baidu.lbsyun:BaiduMapSDK_Location:9.1.8'
    // 权限申请
    implementation 'com.guolindev.permissionx:permissionx:1.6.1'
}